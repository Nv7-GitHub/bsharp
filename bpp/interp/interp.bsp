[IMPORT "bpp/types"]

[TYPE $ICTX [STRUCT 
    [FIELD .vars $MAP] # map[string]Node
    [FIELD .rand $RAND]
]]

[FUNC @INTERP_NODE [[PARAM !n $NODE] [PARAM !c $ICTX] [RETURNS $NODE]] [
    [MATCH !n
        [CASE !v $NODE_STRING [[RETURN !n]]]
        [CASE !v $NODE_INT [[RETURN !n]]]
        [CASE !v $NODE_FLOAT [[RETURN !n]]]
        [CASE !v $NODE_ARRAY [[RETURN !n]]]
        [CASE !v $NODE_TAG [
            [DEFINE !args [GET !v .values]]
            # Block statement?
            [MATCH [GET !v .name]
                [CASE "IF" [[RETURN [@IF !args !c]]]]
            ]

            # Interpret args
            [DEFINE !iargs [NEW [ARRAY $NODE] [LEN !args]]]
            [DEFINE !i 0]
            [WHILE [< !i [LEN !args]] [
                [DEFINE !res [@INTERP_NODE [GET !args !i] !c]]
                [APPEND !iargs !res]
                [DEFINE !i [+ !i 1]]
            ]]         
            [MATCH [GET !v .name]
                [CASE "VAR" [[RETURN [@VAR [GET !iargs 0] !c]]]]
                [CASE "DEFINE" [[RETURN [@DEFINE !iargs !c]]]]
                [CASE "MATH" [[RETURN [@MATH !iargs !c]]]]
                [CASE "RANDINT" [[RETURN [@RANDINT !iargs !c]]]]
                [CASE "RANDOM" [[RETURN [@RANDOM !iargs !c]]]]
                [CASE "FLOOR" [[RETURN [@IFLOOR [GET !iargs 0] !c]]]]
                [CASE "CEIL" [[RETURN [@ICEIL [GET !iargs 0] !c]]]]
                [CASE "INDEX" [[RETURN [@INDEX !iargs !c]]]]
                [CASE "COMPARE" [[RETURN [@COMPARE !iargs !c]]]]
            ]
            [PRINT [@CONCAT [ARR "UNIMPLEMENTED: " [GET !v .name]]]]
            [RETURN [NEW $NODE [NEW $NONE]]]
        ]]
    ]
]]

[FUNC @INTERP [[PARAM !src [ARRAY $NODE]] [RETURNS [ARRAY $NODE]]] [
    [DEFINE !res [NEW [ARRAY $NODE] [LEN !src]]]
    [DEFINE !ctx [NEW $ICTX [: .vars [@MAP_NEW]] [: .rand [@RAND_NEW]]]]
    [DEFINE !i 0]
    [WHILE [< !i [LEN !src]] [
        [DEFINE !r [@INTERP_NODE [GET !src !i] !ctx]]
        [APPEND !res !r]
        [DEFINE !i [+ !i 1]]
    ]]
    [RETURN !res]
]]